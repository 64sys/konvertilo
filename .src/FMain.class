' Gambas class file

'
' Konvertilo
' Conversor y manipulador de archivos
'
' Copyright (C) Martín Belmonte
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
'

'### EVENTOS ###
Public Sub trvEntities_Drop()

  cmdFiles()

End

Public Sub tobAbout_Click()

  FAbout.ShowModal()

End

Public Sub tobConfig_Click()

  FConfig.ShowModal()

End

Public Sub tobStart_Click()

  Dim obj As Object
  Dim strFile As String
  Dim strTool As String
  Dim intPage As Integer

  Dim strReturn As String
  Dim c As Integer
  Dim r As Integer
  Dim stxPDFDir As New String[]
  Dim intS As Integer
  Dim floS As Float ' Valor de avance de la progressbar

  Dim intTime As Integer ' Tiempo promedio para procesar un archivo
  Dim intTimeTot As Integer ' Tiempo esimado para hacer todo el trabajo
  Dim intCurrTime As Integer ' Tiempo transcurrido hasta el momento

  Dim intTotal As Integer
  Dim stxTime As New Integer[]
  Dim strMod As String
  Dim strExt As String
  Dim intN As Integer
  Dim intColor As Integer
  Dim intRow As Integer
  Dim intCol As Integer
  Dim intKey As Integer

  Inc Application.Busy

  Select MStarter.strState
    Case "stopped"

      For Each obj In pnlButtons.Children
        If Object.Type(obj) = "ToggleButton" Then
          If obj.Value = True Then
            MStarter.stxExtPlay.Add(obj.Tag)
          Else
            intKey = MStarter.stxExtPlay.Find(obj.Tag)

            If intKey > -1 Then
              MStarter.stxExtPlay.Remove(intKey)
            Endif
          Endif
        Endif
      Next

      MStarter.strState = "running"
      tobStart.Picture = Picture["icon:/22/stop"]

    Case "running"
      MStarter.stxExtPlay.Clear
      MStarter.strState = "stopped"
      tobStart.Picture = Picture["icon:/22/play"]

  End Select

  ' Antes de comenzar puesta a cero de las listas de archivos
  MStarter.stxErr.Clear
  MStarter.stxEmail.Clear
  MStarter.stxPDF.Clear
  stxPDFDir.Clear

  ' Puesta a cero del gridview
  For c = 0 To grwData.Columns.Max
    For r = 0 To grwData.Rows.Max
      grwData[r, c].Foreground = Color.Foreground
    Next
  Next

  MStarter.stxSchedule = Scheduler(MStarter.stxFiles, MStarter.stxToolsName, MStarter.stxToolsExt, MStarter.stxExtPlay)

  tmrCounter.Start

  'Comienzo del proceso por lotes.
  For intS = 0 To MStarter.stxSchedule.Max

    Print MStarter.stxSchedule[intS]
    sirProcess.Start
    sirProcess.Visible = True
    intTotal = MStarter.stxSchedule.Count
    intCurrTime = MStarter.intTimer

    intTime = intCurrTime / (intS + 1)
    intTimeTot = (intTime * intTotal) - intCurrTime

    floS = floS + (1 / intTotal)
    prsBar.Value = floS

    strTool = Split(MStarter.stxSchedule[intS], "\t")[0]
    strFile = Split(MStarter.stxSchedule[intS], "\t")[1]
    intPage = CInt(Split(MStarter.stxSchedule[intS], "\t")[2])
    strExt = File.Ext(strFile)

    strMod = Settings[String.LCase(strExt) &/ strTool, ""]

    strReturn = MBatch.FileDelibery(strTool, strFile, intPage, strMod)

    While Exist(strReturn) = False
      Wait 0.01
    Wend

    stxTime.Clear

    intCurrTime = MStarter.intTimer

    intTime = intCurrTime / (intS + 1)

    intTimeTot = intTime * intTotal

    stxTime = MUtility.SecondsToHMS(intTimeTot - intCurrTime)

    lblInformation.Text = (strReturn)
    lblTotals.Text = CStr(intS + 1) & " : " & CStr(intTotal)

    lblEstimated.Text = "Falta" & ": " & CStr(stxTime[0]) & ":" & CStr(stxTime[1]) & ":" & CStr(stxTime[2])

    ' Coloreando la fila que esta en proceso
    For intN = 0 To grwData.Rows.Max
      If Split(MStarter.stxSchedule[intS], "\t")[1] = grwData[intN, 0].Text
        intRow = intN
        intCol = 3 ' Columna de extension

        Select intS
          Case MStarter.stxSchedule.Max
            intColor = Color.Red
          Case Else
            If intS < MStarter.stxSchedule.Max Then

              If Split(MStarter.stxSchedule[intS + 1], "\t")[1] = grwData[intN, 0].Text Then
                intColor = Color.Green
              Else
                intColor = Color.Red

              Endif

            Else
              intColor = Color.Red

            Endif

        End Select

      Endif

    Next

    grwData[intRow, intCol].Foreground = intColor

  Next

  ' Finalizacion del proceso e indicacion en el formulario mediante cambio de icono
  MStarter.stxExtPlay.Clear
  MStarter.strState = "stopped"
  tobStart.Picture = Picture["icon:/22/play"]

  sirProcess.Stop
  sirProcess.Visible = False

  lblInformation.Text = (("Proceso finalizado") & ", " & ("tiempo empleado") & ": " & CStr(MStarter.intTimer))

  tmrCounter.Stop
  MStarter.intTimer = 0

  Dec Application.Busy

End

Public Function Scheduler(stxFiles As String[], stxTool As String[], stxExt As String[], stxExtPlay As String[]) As String[] '' stxFiles es la lista de archivos, stxTool es la lista de herramientas, stxExt es la lista de extensiones a las que aplicar las herramientas y estas dos ultimas listas trabajan juntas cada indice se corresponde en ambas listas. stxExtPlay es la lista de extensiones de archivo habilitadas para el procesado.

  Dim stxSch As New String[]
  Dim int1 As Integer
  Dim int2 As Integer
  Dim int3 As Integer
  Dim strExtension As String
  Dim stxVTool As New String[] ' Herramienta válida
  Dim stxPagesTmp As New String[] ' Logística de las páginas
  Dim intM As Integer ' Número de posicion en la cadena de logistica de páginas
  Dim stxExtTmp As New String[]

  stxVTool.Clear

  For int1 = 0 To stxTool.Max

    stxExtTmp.Clear

    If InStr(stxExt[int1], ":") > 0 Then
      stxExtTmp = Split(stxExt[int1], ":")
    Else
      stxExtTmp.Add(stxExt[int1])
    Endif

    For int2 = 0 To stxFiles.Max
      strExtension = String.LCase(File.Ext(stxFiles[int2]))

      If stxExtPlay.Find(String.LCase(File.Ext(stxFiles[int2]))) > -1 Then

        If stxExtTmp.Find(String.LCase(File.Ext(stxFiles[int2]))) > -1 Then ' = stxExt[int1] Then  ' Comprobar si la extension de archivo tiene acciones planeadas

          If Settings[stxTool[int1] &/ strExtension] = True Then ' Comprobar si la herramienta esta seleccionada

            ' Genero una matriz temporal de la logística de páginas
            stxPagesTmp.Clear
            For intM = 1 To Len(MStarter.stxFilesPages[int2])
              stxPagesTmp.Add(Mid(MStarter.stxFilesPages[int2], intM, 1))
            Next

            For int3 = 0 To stxPagesTmp.Max
              If stxPagesTmp[int3] = "1" Then
                stxSch.Add(stxTool[int1] & "\t" & stxFiles[int2] & "\t" & CStr(int3))
              Endif

            Next

          Endif
        Endif
      Endif
    Next

  Next

  ' Acá ya se tiene la lista de trabajo completa y, por lo tanto se puede configurar una barra de progreso por ejemplo.
  Return stxSch

End

Public Sub FileExtButton_Click()

  Dim tgb As ToggleButton
  Dim strText As String

  tgb = Last

  If tgb.Value = True Then

    strText = tgb.Tag

    FTools.Run(strText)

  Else
    Print "Desactivado " & strText
  Endif

End

Public Sub tobHelp_Click()

  Dim strFile As String

  strFile = MStarter.strAppPath &/ "README.md"
  If Exist(strFile)
    Desktop.Open(strFile)
  Endif

End

'##############################################################################

Public Sub grwData_Drop()

  Dim stxPaths As New String[]

  Inc Application.Busy
  stxPaths = MUtility.FilesList(Drag.Paste("text/uri-list"))
  MStarter.stxFiles = cmdFiles(stxPaths)

  If MStarter.stxFiles.Count > 0 Then
    cmdCheckFiles()
  Endif
  Dec Application.Busy

End

'### FUNCIONES ###

Public Function cmdFiles(stxPaths As String[]) As String[]

  Dim strPath As String
  'Dim stxPaths As New String[]
  Dim stxFilesTmp As New String[]
  Dim stxFiles As New String[]
  Dim strPathTmp As String

  'stxPaths = MUtility.FilesList(Drag.Paste("text/uri-list"))

  stxFiles = MStarter.stxFiles

  ' En este punto se tiene la lista de archivos
  ' Para cada ruta de archivo se verifica que exista y se extraen los metadaos de este
  For Each strPath In stxPaths

    strPath = MUtility.ConvertPath(strPath) ' Para caracteres con acento

    If Exist(strPath) Then

      Select Stat(strPath).Type

        Case gb.File
          If stxFiles.Find(strPath) = -1 Then
            stxFiles.Add(strPath)
          Endif

        Case gb.Directory

          stxFilesTmp.Clear
          stxFilesTmp = MUtility.ScanFolder(strPath)

          For Each strPathTmp In stxFilesTmp
            If stxFiles.Find(strPathTmp) = -1 Then
              stxFiles.Add(strPathTmp)
            Endif
          Next

      End Select

    Endif
  Next
  ' Orden de la lista
  If stxFiles.Count > 0 Then
    stxFiles.Sort
  Endif

  Return stxFiles

  'cmdCheckFiles()

End

Public Sub cmdCheckFiles() ' Hace dos cosas: Cargar el gridview y cargar el Treeview.

  Dim stxFInfo As New String[]
  Dim intF As Integer
  Dim intI As Integer
  Dim intH As Integer
  Dim intT As Integer
  Dim strExtTmp As String
  Dim btn As ToggleButton
  Dim intPg As Integer
  Dim intPages As Integer
  Dim strPages As String

  grwData.Header = 3
  grwData.Grid = True
  grwData.Columns.Count = 10

  For intH = 0 To MStarter.stxHeaders.Max
    grwData.Columns[intH].Text = MStarter.stxHeaders[intH]
    Select intH
      Case 0, 1, 2, 5, 6, 7, 8
        grwData.Columns[intH].Width = 0
      Case 3, 4 ' Solo muestro las columnas de extensión y de nombre del archivo
        grwData.Columns[intH].Width = Settings["GridColumn/" & CStr(intH), 75]
    End Select
    '     grwData.Columns[intH].Width = 50
  Next

  grwData.Rows.Count = 0
  grwData.Rows.Count = MStarter.stxFiles.Count

  MStarter.stxExt.Clear
  MStarter.stxFilesPages.Clear

  For intI = 0 To MStarter.stxFiles.Max
    stxFInfo.Clear
    'MStarter.stxFilesPages.Clear

    'Extraccion de la informacion del arcivo
    stxFInfo = MUtility.FileInfo(MStarter.stxFiles[intI])
    'Cantidad de páginas
    stxFInfo.Add("CountPages" & "\t" & MUtility.ExifPages(MStarter.stxFiles[intI]))

    strPages = ""

    For intF = 0 To stxFInfo.Max

      Select Split(stxFInfo[intF], "\t")[0]

        Case "FileExt"
          strExtTmp = String.LCase(Split(stxFInfo[intF], "\t")[1])
          If strExtTmp <> "" Then
            If MStarter.stxExt.Find(strExtTmp) = -1 Then
              MStarter.stxExt.Add(strExtTmp)
            Endif
          Endif

          grwData[intI, intF].Text = Split(stxFInfo[intF], "\t")[1]

        Case "CountPages"
          intPages = CInt(Split(stxFInfo[intF], "\t")[1])

          For intPg = 0 To intPages - 1
            strPages &= "1"
          Next

          MStarter.stxFilesPages.Add(strPages)
          grwData[intI, intF].Text = strPages

        Case Else

          grwData[intI, intF].Text = Split(stxFInfo[intF], "\t")[1]

      End Select
    Next
  Next

  'Rellenar el treeview
  trvEntities.Clear

  trvEntities.Add("fileextension", ("Extensión"))
  trvEntities["fileextension"].Expanded = True

  pnlButtons.Children.Clear

  For intT = 0 To MStarter.stxExt.Max
    trvEntities.Add(MStarter.stxExt[intT], MStarter.stxExt[intT],, "fileextension")

    btn = New ToggleButton(pnlButtons) As "FileExtButton"
    With btn
      .Name = MStarter.stxExt[intT]
      .Text = String.UCase(MStarter.stxExt[intT])
      .Tag = String.LCase(MStarter.stxExt[intT])
      .Width = ((String.Len(MStarter.stxExt[intT])) * 8) + 16
      .Height = 28

    End With

  Next

End

Public Sub tobClean_Click()

  MStarter.stxFiles.Clear
  MStarter.stxEmail.Clear
  MStarter.stxPDF.Clear
  lblEstimated.Text = ""
  lblInformation.Text = ""
  lblTotals.Text = ""

  MStarter.stxExtPlay.Clear
  MStarter.strState = "stopped"

  trvEntities.Clear
  grwData.Clear
  grwData.Rows.Count = 0
  pnlButtons.Children.Clear

  tobStart.Picture = Picture["icon:/22/play"]
  lblInformation.Text = ""

End

Public Function RemovePaths(stxPaths As String[]) As Integer

  Dim strPath As String
  'Dim strExt As String
  Dim intKeyFile As Integer
  'Dim intRow As Integer

  If stxPaths.Count > 0 Then
    For Each strPath In stxPaths
      intKeyFile = MStarter.stxFiles.Find(strPath)
      If intKeyFile > -1 Then
        MStarter.stxFiles.Remove(intKeyFile)
      Endif
    Next
  Endif

  lblEstimated.Text = ""
  lblInformation.Text = ""
  lblTotals.Text = ""

  MStarter.stxExtPlay.Clear
  MStarter.strState = "stopped"

  trvEntities.Clear

  grwData.Rows.Count = 0
  pnlButtons.Children.Clear

  tobStart.Picture = Picture["icon:/22/play"]
  lblInformation.Text = ""

  cmdCheckFiles()

  Return 1

End

Public Sub Form_Open()

  HSplit1.Layout = [1, 5]
  sirProcess.Stop
  sirProcess.Visible = False

End

Public Sub tmrCounter_Timer()

  Inc MStarter.intTimer

End

Public Sub grwData_ColumnResize(Column As Integer)

  Settings["GridColumn/" & CStr(Column)] = grwData.Columns[Column].Width

End

Public Sub grwData_DblClick()

  Dim inx As New Integer[]
  Dim inxMod As New Integer[]
  Dim strPages As String
  Dim strFile As String
  Dim int As Integer

  strFile = grwData[grwData.Row, 0].Text
  strPages = grwData[grwData.Row, 8].Text

  MStarter.stxPages.Clear

  For int = 1 To String.Len(strPages)
    MStarter.stxPages.Add(CInt(String.Mid(strPages, int, 1)))
  Next

  inxMod = MStarter.stxPages

  FPages.Run(inxMod)

  Wait 0.01
  Print "control 1"

  strPages = ""
  If inx <> MStarter.stxPages Then
    For int = 0 To MStarter.stxPages.Max
      strPages &= MStarter.stxPages[int]
    Next

    MStarter.stxFilesPages[MStarter.stxFiles.Find(strFile)] = strPages

    grwData[grwData.Row, 8].Text = strPages
    grwData[grwData.Row, 8].Refresh

  Endif

  Print "control 2"

End

Public Sub tobAddDir_Click()

  Dim strDirectory As String
  Dim stxPaths As New String[]

  strDirectory = MUtility.DirChooser()
  If Exist(strDirectory) Then
    If Stat(strDirectory).Type = gb.Directory Then

      Inc Application.Busy

      stxPaths = MUtility.FilesList(strDirectory)

      MStarter.stxFiles = cmdFiles(stxPaths)

      If MStarter.stxFiles.Count > 0 Then
        cmdCheckFiles()
      Endif
      Dec Application.Busy

    Endif
  Endif

End

Public Sub tobAddFiles_Click()

  Dim stxFiles As New String[]
  Dim strFiles As String
  Dim stxPaths As New String[]

  stxFiles = MUtility.FileChooser()

  If stxFiles.Count > 0 Then

    strFiles = stxFiles.Join("\n")

    If Exist(strFiles) Then
      If Stat(strFiles).Type = gb.File Then
        Inc Application.Busy

        stxPaths = MUtility.FilesList(strFiles)

        MStarter.stxFiles = cmdFiles(stxPaths)

        If MStarter.stxFiles.Count > 0 Then
          cmdCheckFiles()
        Endif
        Dec Application.Busy

      Endif
    Endif

  Endif

End

Public Sub tobClean2_Click()

  Dim intKey As Integer
  Dim intAction As Integer
  Dim strPath As String
  Dim intAll As Integer
  Dim strAll As String
  Dim stxPaths As New String[]
  Dim stxAll As New String[]

  For Each intKey In grwData.Rows.Selection
    strPath = grwData[intKey, 0].Text
    stxPaths.Add(strPath)
  Next

  For intAll = 0 To grwData.Rows.Max
    strAll = grwData[intAll, 0].Text
    stxAll.Add(strAll)
  Next

  If stxPaths.Count > 0 Then
    RemovePaths(stxPaths)
  Else
    intAction = Message.Warning(("¿Quiere quitar todos los archivos de la lista?"), ("Aceptar"), ("Cancelar"))
    Select intAction
      Case 1
        RemovePaths(stxAll)
        'Case 2

    End Select

  Endif

End
