' Gambas class file

'
' Konvertilo
' Conversor y manipulador de archivos
'
' Copyright (C) Martín Belmonte
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
'

'### EVENTOS ###
Public Sub trvEntities_Drop()

  cmdFiles()

End

Public Sub tobAbout_Click()

  FAbout.ShowModal()

End

Public Sub tobConfig_Click()

  FConfig.ShowModal()

End

Public Sub tobStart_Click()

  Dim obj As Object
  Dim strFile As String
  Dim strTool As String
  Dim intPage As Integer

  Dim strReturn As String
  Dim c As Integer
  Dim r As Integer
  Dim stxPDFDir As New String[]
  Dim intS As Integer
  Dim floS As Float ' Valor de avance de la progressbar

  Dim intTime As Integer ' Tiempo promedio para procesar un archivo
  Dim intTimeTot As Integer ' Tiempo esimado para hacer todo el trabajo
  Dim intCurrTime As Integer ' Tiempo transcurrido hasta el momento

  Dim intTotal As Integer
  Dim stxTime As New Integer[]

  Inc Application.Busy

  Select MStarter.strState
    Case "stopped"

      For Each obj In pnlButtons.Children
        If Object.Type(obj) = "ToggleButton" Then
          If obj.Value = True Then
            MStarter.stxFilesPlay.Add(obj.Tag)
          Endif
        Endif
      Next

      MStarter.strState = "running"
      tobStart.Picture = Picture["icon:/22/stop"]

    Case "running"
      MStarter.stxFilesPlay.Clear
      MStarter.strState = "stopped"
      tobStart.Picture = Picture["icon:/22/play"]

  End Select

  ' Antes de comenzar puesta a cero de las listas de archivos
  MStarter.stxErr.Clear
  MStarter.stxEmail.Clear
  MStarter.stxPDF.Clear
  stxPDFDir.Clear

  ' Puesta a cero del gridview
  For c = 0 To grwData.Columns.Max
    For r = 0 To grwData.Rows.Max
      grwData[r, c].Foreground = Color.Foreground
    Next
  Next

  MStarter.stxSchedule = Scheduler(MStarter.stxFiles, MStarter.stxToolsName, MStarter.stxToolsExt)

  tmrCounter.Start

  'Comienzo del proceso por lotes.
  For intS = 0 To MStarter.stxSchedule.Max

    Print MStarter.stxSchedule[intS]
    sirProcess.Start
    sirProcess.Visible = True
    intTotal = MStarter.stxSchedule.Count
    intCurrTime = MStarter.intTimer

    intTime = intCurrTime / (intS + 1)
    intTimeTot = (intTime * intTotal) - intCurrTime

    floS = floS + (1 / intTotal)
    prsBar.Value = floS

    strTool = Split(MStarter.stxSchedule[intS], "\t")[0]
    strFile = Split(MStarter.stxSchedule[intS], "\t")[1]

    intPage = CInt(Split(MStarter.stxSchedule[intS], "\t")[2])

    strReturn = MBatch.FileDelibery(strTool, strFile, intPage)

    stxTime.Clear

    intCurrTime = MStarter.intTimer

    intTime = intCurrTime / (intS + 1)

    intTimeTot = intTime * intTotal

    stxTime = MUtility.SecondsToHMS(intTimeTot - intCurrTime)

    lblInformation.Text = (strReturn)
    lblTotals.Text = CStr(intS + 1) & " : " & CStr(intTotal)

    lblEstimated.Text = "Falta" & ": " & CStr(stxTime[0]) & ":" & CStr(stxTime[1]) & ":" & CStr(stxTime[2])

  Next
  ' Finalizacion del proceso e indicacion en el formulario mediante cambio de icono
  MStarter.stxFilesPlay.Clear
  MStarter.strState = "stopped"
  tobStart.Picture = Picture["icon:/22/play"]

  sirProcess.Stop
  sirProcess.Visible = False

  lblInformation.Text = (("Proceso finalizado") & ", " & ("tiempo empleado") & ": " & CStr(MStarter.intTimer))

  tmrCounter.Stop
  MStarter.intTimer = 0

  Dec Application.Busy

End

Public Function Scheduler(stxFiles As String[], stxTool As String[], stxExt As String[]) As String[] '' stxFiles es la lista de archivos, stxTool es la lista de herramientas, stxExt es la lista de extensiones a las que aplicar las herramientas y estas dos ultimas listas trabajan juntas cada indice se corresponde en ambas listas.

  Dim stxSch As New String[]
  Dim int1 As Integer
  Dim int2 As Integer
  Dim int3 As Integer
  Dim strExtension As String
  Dim stxVTool As New String[] ' Herramienta válida
  Dim stxPagesTmp As New String[] ' Logística de las páginas
  Dim intM As Integer ' Número de posicion en la cadena de logistica de páginas

  stxVTool.Clear

  For int1 = 0 To stxTool.Max

    For int2 = 0 To stxFiles.Max
      strExtension = String.LCase(File.Ext(stxFiles[int2]))

      If File.Ext(stxFiles[int2]) = stxExt[int1] Then  ' Comprobar si la extension de archivo tiene acciones planeadas
        If Settings[strExtension & "/" & stxTool[int1]] = True Then ' Comprobar si la herramienta esta seleccionada

          ' Genero una matriz temporal de la logística de páginas
          stxPagesTmp.Clear
          For intM = 1 To Len(MStarter.stxFilesPages[int2])
            stxPagesTmp.Add(Mid(MStarter.stxFilesPages[int2], intM, 1))
          Next

          For int3 = 0 To stxPagesTmp.Max
            If stxPagesTmp[int3] = "1" Then
              stxSch.Add(stxTool[int1] & "\t" & stxFiles[int2] & "\t" & CStr(int3))
            Endif

          Next

        Endif
      Endif
    Next

  Next

  ' Acá ya se tiene la lista de trabajo completa y, por lo tanto se puede configurar una barra de progreso por ejemplo.
  Return stxSch

End

Public Sub FileExtButton_Click()

  Dim tgb As ToggleButton

  tgb = Last

  If tgb.Value = True Then

    FTools.Run(tgb.Text)

  Else
    Print "Desactivado " & tgb.Text
  Endif

End

Public Sub tobHelp_Click()

  Dim strFile As String

  strFile = MStarter.strAppPath &/ "README.md"
  If Exist(strFile)
    Desktop.Open(strFile)
  Endif

End

'##############################################################################

Public Sub grwData_Drop()

  Inc Application.Busy
  cmdFiles()
  Dec Application.Busy

End

'### FUNCIONES ###

Public Function cmdFiles() As String[]

  Dim strPath As String
  Dim stxPaths As New String[]
  Dim stxFilesTmp As New String[]
  Dim strPathTmp As String

  stxPaths = MUtility.FilesList(Drag.Paste("text/uri-list"))

  ' En este punto se tiene la lista de archivos
  ' Para cada ruta de archivo se verifica que exista y se extraen los metadaos de este
  For Each strPath In stxPaths
    If Exist(strPath) Then

      Select Stat(strPath).Type

        Case gb.File
          If MStarter.stxFiles.Find(strPath) = -1 Then
            MStarter.stxFiles.Add(strPath)
          Endif

        Case gb.Directory

          stxFilesTmp.Clear
          stxFilesTmp = MUtility.ScanFolder(strPath)

          For Each strPathTmp In stxFilesTmp
            If MStarter.stxFiles.Find(strPathTmp) = -1 Then
              MStarter.stxFiles.Add(strPathTmp)
            Endif
          Next

      End Select

    Endif
  Next
  ' Orden de la lista
  If MStarter.stxFiles.Count > 0 Then
    MStarter.stxFiles.Sort
  Endif

  cmdCheckFiles()

End

Public Sub cmdCheckFiles() ' Hace dos cosas: Cargar el gridview y cargar el Treeview.

  Dim stxFInfo As New String[]
  Dim intF As Integer
  Dim intI As Integer
  Dim intH As Integer
  Dim intT As Integer
  Dim strExtTmp As String
  Dim btn As ToggleButton
  Dim intPg As Integer
  Dim intPages As Integer
  Dim strPages As String

  grwData.Header = 3
  grwData.Grid = True
  grwData.Columns.Count = 10

  For intH = 0 To MStarter.stxHeaders.Max
    grwData.Columns[intH].Text = MStarter.stxHeaders[intH]
    Select intH
      Case 0, 1, 2, 5, 6, 7, 8
        grwData.Columns[intH].Width = 0
      Case 3, 4 ' Solo muestro las columnas de extensión y de nombre del archivo
        grwData.Columns[intH].Width = Settings["GridColumn/" & CStr(intH), 75]
    End Select
    '     grwData.Columns[intH].Width = 50
  Next

  grwData.Rows.Count = 0
  grwData.Rows.Count = MStarter.stxFiles.Count

  MStarter.stxExt.Clear

  For intI = 0 To MStarter.stxFiles.Max
    stxFInfo.Clear
    MStarter.stxFilesPages.Clear

    'Extraccion de la informacion del arcivo
    stxFInfo = MUtility.FileInfo(MStarter.stxFiles[intI])
    'Cantidad de páginas
    stxFInfo.Add("CountPages" & "\t" & MUtility.ExifPages(MStarter.stxFiles[intI]))

    strPages = ""

    For intF = 0 To stxFInfo.Max

      Select Split(stxFInfo[intF], "\t")[0]

        Case "FileExt"
          strExtTmp = String.LCase(Split(stxFInfo[intF], "\t")[1])
          If strExtTmp <> "" Then
            If MStarter.stxExt.Find(strExtTmp) = -1 Then
              MStarter.stxExt.Add(strExtTmp)
            Endif
          Endif

          grwData[intI, intF].Text = Split(stxFInfo[intF], "\t")[1]

        Case "CountPages"
          intPages = CInt(Split(stxFInfo[intF], "\t")[1])

          For intPg = 0 To intPages - 1
            strPages &= "1"
          Next

          MStarter.stxFilesPages.Add(strPages)
          grwData[intI, intF].Text = strPages

        Case Else

          grwData[intI, intF].Text = Split(stxFInfo[intF], "\t")[1]

      End Select
    Next
  Next

  'Rellenar el treeview
  trvEntities.Clear

  trvEntities.Add("fileextension", ("Extensión"))
  trvEntities["fileextension"].Expanded = True

  pnlButtons.Children.Clear

  For intT = 0 To MStarter.stxExt.Max
    trvEntities.Add(MStarter.stxExt[intT], MStarter.stxExt[intT],, "fileextension")

    btn = New ToggleButton(pnlButtons) As "FileExtButton"
    With btn
      .Name = MStarter.stxExt[intT]
      .Text = String.UCase(MStarter.stxExt[intT])
      .Tag = String.LCase(MStarter.stxExt[intT])
      .Width = ((String.Len(MStarter.stxExt[intT])) * 8) + 16
      .Height = 28

    End With

  Next

End

Public Sub tobClean_Click()

  MStarter.stxFiles.Clear
  MStarter.stxEmail.Clear
  MStarter.stxPDF.Clear

  MStarter.stxFilesPlay.Clear
  MStarter.strState = "stopped"

  trvEntities.Clear
  grwData.Clear
  grwData.Rows.Count = 0
  pnlButtons.Children.Clear

  tobStart.Picture = Picture["icon:/22/play"]
  lblInformation.Text = ""

End

Public Sub Form_Open()

  HSplit1.Layout = [1, 5]
  sirProcess.Stop
  sirProcess.Visible = False

End

Public Sub tmrCounter_Timer()

  Inc MStarter.intTimer

End

Public Sub grwData_ColumnResize(Column As Integer)

  Settings["GridColumn/" & CStr(Column)] = grwData.Columns[Column].Width

End

Public Sub grwData_DblClick()

  Dim inx As New Integer[]
  Dim inxMod As New Integer[]
  Dim str As String
  Dim strFile As String
  Dim int As Integer

  str = grwData[grwData.Row, 8].Text
  strFile = grwData[grwData.Row, 0].Text

  MStarter.stxPages.Clear

  For int = 1 To String.Len(str)
    MStarter.stxPages.Add(CInt(String.Mid(str, int, 1)))
  Next

  inxMod = MStarter.stxPages

  FPages.Run(inxMod)

  Wait 0.01
  Print "control 1"

  str = ""
  If inx <> MStarter.stxPages Then
    For int = 0 To MStarter.stxPages.Max
      str &= MStarter.stxPages[int]
    Next

    MStarter.stxFilesPages[MStarter.stxFiles.Find(strFile)] = str

    grwData[grwData.Row, 8].Text = str
    grwData[grwData.Row, 8].Refresh

  Endif

  Print "control 2"

End
